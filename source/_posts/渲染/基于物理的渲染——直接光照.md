---
title: 基于物理的渲染——直接光照
categories: 渲染
mathjax: true
---
上篇文章的最后引入了PBR的渲染方程：
$$
L_o = \int_{\Omega }(\frac{c_{diff}}{π}+ \frac{D(h)G(\omega_i, \omega_o,h)F(\omega_o,h)}{4(n\cdot \omega_i)(n\cdot \omega_o)})\otimes L_i(\omega_i)\cos\theta_id\omega_i
$$
在这篇文章里将介绍PBR在方向光源下的着色模型。由于是方向光源，物体表面的每一点只会被一束光照到，所以方向光源的渲染方程可以写成：
$$
L_o = (\frac{c_{diff}}{π}+ \frac{D(h)G(\omega_i, \omega_o,h)F(\omega_o,h)}{4(n\cdot \omega_i)(n\cdot \omega_o)})\otimes L_i(\omega_i)\cos\theta_i
$$
其中$L_i$可以用光的颜色表示，我们只需要求出漫反射部分$\frac{c_{diff}}{π}$和高光反射部分$\frac{D(h)G(\omega_i, \omega_o,h)F(\omega_o,h)}{4(n\cdot \omega_i)(n\cdot \omega_o)}$即可。
<!-- more --> 
### 一、变量介绍
解析PBR渲染方程需要引入几个变量，下面做一下介绍。
#### roughness（粗糙度）
表示表面的粗糙程度，主要影响高光反射的计算，高光反射BRDF模型的D、G都跟roughness相关。
#### metallic（金属度）
理论上一个物体的金属度非0即1。材质的纹理精度不足以描述一个如细沙/有刮痕的金属表面，然而通过在(0, 1)范围内调节金属度，可以取得非常好的模拟效果。
#### albedo（反射率）
[菲涅尔方程](https://zh.wikipedia.org/wiki/%E8%8F%B2%E6%B6%85%E8%80%B3%E6%96%B9%E7%A8%8B)是一个相当复杂的方程式。不过可以根据Fresnel-Schlick近似法求的近似解：
$$
F_{Schlick}(n, v, F_0) = F_0 + (1 - F_0) ( 1 - (n \cdot v))^5
$$
其中$F0$表示平面的基础反射率。下图是常见物质的基础反射率。
![](http://ww1.sinaimg.cn/mw690/c5c3a364ly1fyl37ymoyaj20hj0augnf.jpg)
从这个表里我们可以观察到，所有非金属的基础反射率大多不会高于0.17，而导体材质表面的基础反射率大多在0.5和1.0之间。此外，对于导体或者金属表面而言基础反射率一般是带有色彩的，这也是为什么$F0$要用RGB三原色来表示的原因。

对在金属度(0, 1)范围内的物质，其基础反射率可以用插值近似。为了方便计算，所有物质的基础反射率计算采用一个公式。我们为非金属定义了一个近似的基础反射率0.04，当金属度为0时，采用反射率0.04，当金属度为1时，采用输入的折射率albedo。
``` glsl
vec3 F0 = vec3(0.04);
F0 = mix(F0, albedo.rgb, metalness);
```

### 二、specular BRDF
我们先看BRDF的高光反射部分：$\frac{D(h)G(\omega_i, \omega_o,h)F(\omega_o,h)}{4(n\cdot \omega_i)(n\cdot \omega_o)}$。需要对D、G、F分别建立模型。

#### 法线分布函数（NDF）
法线分布函数$D$，从统计学上近似地表示了微平面中法线与中间向量$h$一致的比率。我们使用Trowbridge-Reitz GGX模型获取这个比率。
$$
NDF_{GGX TR}(n, h, \alpha) = \frac{\alpha^2}{\pi((n \cdot h)^2 (\alpha^2 - 1) + 1)^2}
$$
其中α表示粗糙度。

当粗糙度很低时，NDF很集中，所以会有一个非常明亮的斑点。当表面比较粗糙的时候，微平面的方向更加随机，NDF的值很发散，所以高光也比较分散。UE4对这个方程做了一点修改，将输入的粗糙度平方，使光照更加自然。

不同粗糙度下的效果如下图：
![](http://ww1.sinaimg.cn/mw690/c5c3a364ly1fymqvlu6dmj20m804jaa6.jpg)

用GLSL写成的shader代码如下：
```glsl
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a      = roughness*roughness;
    float a2     = a*a;
    float NdotH  = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}
```
#### 几何遮挡函数
几何遮挡函数$G$从统计学上求得了微平面间相互遮挡的比率。与NDF一样，采用粗糙度作为参数。粗糙度越大，几何遮挡的概率就越高。我们使用的几何遮挡函数是GGX与Schlick-Beckmann的结合Schlick-GGX。
$$
G_{SchlickGGX}(n, v, k) = \frac{n \cdot v}{(n \cdot v)(1 - k) + k }
$$
这里的k在直接光照中为$k = \frac{(\alpha + 1)^2}{8}$，间接光照中为$k = \frac{\alpha^2}{2}$。
而几何遮挡需要把观察方向和光线方向都考虑进去，用史密斯法将二者结合：
$$
G_{Smith}(n,v,l,k)=G_{SchlickGGX}(n,v,k)G_{SchlickGGX}(n,l,k)
$$
不同粗糙度下的效果如下：
![](http://ww1.sinaimg.cn/mw690/c5c3a364ly1fymrj5shaoj20m804j3ys.jpg)

用GLSL编写的shader代码如下：
``` glsl
float GeometrySchlickGGX(float NdotV, float k)
{
    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float k)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx1 = GeometrySchlickGGX(NdotV, k);
    float ggx2 = GeometrySchlickGGX(NdotL, k);

    return ggx1 * ggx2;
}
```
#### 菲涅尔效果
上面已经给出了Fresnel-Schlick的菲涅尔公式，和不同物质的基础发射率F0的计算方法。
用GLSL写成shader：
```glsl
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
	return F0 + (1.0 - F0) * pow(1.0 - cosTheta, 5.0);
}

vec3 F0 = vec3(0.04);
F0 = mix(F0, albedo.rgb, metalness);
vec3 F  = fresnelSchlick(max(dot(H, V), 0.0), F0);
```
BRDF高光部分需要的D、G、F都已经明了，将其用GLSL写成shader如下：
``` glsl
vec3 nominator		= NDF * G * F;
float denominator	= 4.0 * max(dot(N, V), 0.0) * max(dot(N, L), 0.0) + 0.001; 
vec3 specular		 = nominator / denominator;  
```

### 三、diffuse BRDF
根据能量守恒定律，辐射率中去除高光反射部分，其余的为漫反射部分，即$1.0 - F$。而由于金属不会有漫反射，当金属度为1时，我们把漫反射比率设成0。
$$
f_d = \frac{albedo * (1.0 -F) * (1.0 - metallic)}{π}
$$
用GLSL写成shader：
``` glsl
const float PI = 3.14159265359;
vec3 diffuse = albeo * (1.0 - F) * (1.0 - metallic) / PI;
```
至此，BRDF的diffuse项和specular项都已经以GLSL的代码给出，再结合渲染方程，把光的颜色和宏观法线$N$与光线的角度$L$考虑进去，便得到方向光在某一像素点的辐射率，即最终的颜色。
``` glsl
float NdotL = max(dot(N, L), 0.0);      
vec3 Lo = (diffuse + specuar) * NdotL * lightcolor;
```
### 四、总结
PBR的直接光照非常容易实现，但是需要注意要在线性空间下渲染，才能保证物理的光照规律。线性空间和Gamma校正的知识可以参考[Gamma校正](https://silence394.github.io/2018/07/25/Gamma%E6%A0%A1%E6%AD%A3/)。

本文的代码主要参考[LearnOpenGL](https://learnopengl-cn.github.io/07%20PBR/02%20Lighting/)，强烈推荐！

在最后放两招PBR直接光照的图（加了亮度为0.2的环境光），可以看到没有间接光照的PBR跟一般的材质没有太大的差别，下一节加入间接光照，展现PBR真正的魅力。
![](http://ww1.sinaimg.cn/large/c5c3a364ly1fymtzd8t43j20vd0qxn2j.jpg)
![](http://ww1.sinaimg.cn/large/c5c3a364ly1fymtzd8qrej216y08agq7.jpg)

### 参考
1. https://learnopengl-cn.github.io/07%20PBR/01%20Theory/
2. https://learnopengl-cn.github.io/07%20PBR/01%20Theory/
3. http://graphicrants.blogspot.com/2013/08/specular-brdf-reference.html
4. https://blog.selfshadow.com/publications/s2013-shading-course/